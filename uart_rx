library IEEE;
use IEEE.STD_LOGIC_1164.ALL;
use IEEE.NUMERIC_STD.ALL;

entity uart_rx is
    Generic (
        -- Default 434 untuk 115200bps @ 50MHz
        CLKS_PER_BIT : integer := 434 
    );
    Port (
        i_Clk       : in  STD_LOGIC;
        i_Rst_n     : in  STD_LOGIC;
        i_RX_Serial : in  STD_LOGIC;
        o_RX_DV     : out STD_LOGIC;
        o_RX_Byte   : out STD_LOGIC_VECTOR (7 downto 0)
    );
end uart_rx;

architecture RTL of uart_rx is
    
    type t_SM_Main is (s_Idle, s_RX_Start_Bit, s_RX_Data_Bits, 
                       s_RX_Stop_Bit, s_Cleanup);
    signal r_SM_Main : t_SM_Main := s_Idle;

    signal r_RX_Data_R : std_logic := '1';
    signal r_RX_Data   : std_logic := '1';

    signal r_Clk_Count : integer range 0 to CLKS_PER_BIT-1 := 0;
    signal r_Bit_Index : integer range 0 to 7 := 0;
    signal r_RX_Byte   : std_logic_vector(7 downto 0) := (others => '0');
    signal r_RX_DV     : std_logic := '0';
    
begin

    -- Input Synchronizer (Double Flopping)
    p_Sync : process (i_Clk)
    begin
        if rising_edge(i_Clk) then
            r_RX_Data_R <= i_RX_Serial;
            r_RX_Data   <= r_RX_Data_R;
        end if;
    end process p_Sync;

    -- Main FSM
    p_UART_RX : process (i_Clk, i_Rst_n)
    begin
        if i_Rst_n = '0' then
            r_SM_Main   <= s_Idle;
            r_RX_DV     <= '0';
            r_RX_Byte   <= (others => '0');
            r_Clk_Count <= 0;
            r_Bit_Index <= 0;
        elsif rising_edge(i_Clk) then
            case r_SM_Main is
                when s_Idle =>
                    r_RX_DV       <= '0';
                    r_Clk_Count   <= 0;
                    r_Bit_Index   <= 0;
                    
                    if r_RX_Data = '0' then       -- Start bit detected
                        r_SM_Main <= s_RX_Start_Bit;
                    else
                        r_SM_Main <= s_Idle;
                    end if;

                when s_RX_Start_Bit =>
                    -- Cek middle of start bit
                    if r_Clk_Count = (CLKS_PER_BIT-1)/2 then
                        if r_RX_Data = '0' then   
                            r_Clk_Count <= 0;
                            r_SM_Main   <= s_RX_Data_Bits;
                        else
                            r_SM_Main   <= s_Idle; -- False alarm/Noise
                        end if;
                    else
                        r_Clk_Count <= r_Clk_Count + 1;
                        r_SM_Main   <= s_RX_Start_Bit;
                    end if;

                when s_RX_Data_Bits =>
                    if r_Clk_Count < CLKS_PER_BIT-1 then
                        r_Clk_Count <= r_Clk_Count + 1;
                        r_SM_Main   <= s_RX_Data_Bits;
                    else
                        r_Clk_Count            <= 0;
                        r_RX_Byte(r_Bit_Index) <= r_RX_Data;
                        
                        if r_Bit_Index < 7 then
                            r_Bit_Index <= r_Bit_Index + 1;
                            r_SM_Main   <= s_RX_Data_Bits;
                        else
                            r_Bit_Index <= 0;
                            r_SM_Main   <= s_RX_Stop_Bit;
                        end if;
                    end if;

                when s_RX_Stop_Bit =>
                    -- Tunggu Stop Bit selesai
                    if r_Clk_Count < CLKS_PER_BIT-1 then
                        r_Clk_Count <= r_Clk_Count + 1;
                        r_SM_Main   <= s_RX_Stop_Bit;
                    else
                        r_RX_DV     <= '1'; -- Valid Pulse
                        r_Clk_Count <= 0;
                        r_SM_Main   <= s_Cleanup;
                    end if;

                when s_Cleanup =>
                    r_SM_Main <= s_Idle;
                    r_RX_DV   <= '0';
                    
                when others =>
                    r_SM_Main <= s_Idle;
            end case;
        end if;
    end process p_UART_RX;
    
    o_RX_DV   <= r_RX_DV;
    o_RX_Byte <= r_RX_Byte;
    
end RTL;
