library IEEE;
use IEEE.STD_LOGIC_1164.ALL;
use IEEE.NUMERIC_STD.ALL;

entity uart_tx is
    Generic (
        CLKS_PER_BIT : integer := 5208
    );
    Port (
        i_Clk       : in  STD_LOGIC;
        i_Rst_n     : in  STD_LOGIC;
        i_TX_DV     : in  STD_LOGIC;
        i_TX_Byte   : in  STD_LOGIC_VECTOR (7 downto 0);
        
        o_TX_Active : out STD_LOGIC;
        o_TX_Serial : out STD_LOGIC;
        o_TX_Done   : out STD_LOGIC
    );
end uart_tx;

architecture RTL of uart_tx is
    type t_SM_Main is (s_Idle, s_TX_Start_Bit, s_TX_Data_Bits, 
                       s_TX_Stop_Bit, s_Cleanup);
    signal r_SM_Main : t_SM_Main := s_Idle;

    signal r_Clk_Count : integer range 0 to CLKS_PER_BIT-1 := 0;
    signal r_Bit_Index : integer range 0 to 7 := 0;
    signal r_TX_Data   : std_logic_vector(7 downto 0) := (others => '0');
    signal r_TX_Done   : std_logic := '0';
    signal r_TX_Active : std_logic := '0';
    signal r_TX_Serial : std_logic := '1';
begin

    process (i_Clk, i_Rst_n)
    begin
        if i_Rst_n = '0' then
            r_SM_Main   <= s_Idle;
            r_TX_Done   <= '0';
            r_TX_Active <= '0';
            r_TX_Serial <= '1';
            r_Bit_Index <= 0;
            r_Clk_Count <= 0;
			r_TX_Data   <= (others => '0');
        elsif rising_edge(i_Clk) then
            r_TX_Done <= '0'; 
            case r_SM_Main is

                -- 1. IDLE
                when s_Idle =>
                    r_TX_Active <= '0';
                    r_TX_Serial <= '1';
                    r_Clk_Count <= 0;
                    r_Bit_Index <= 0;
                    
                    if i_TX_DV = '1' then
                        r_TX_Data <= i_TX_Byte;
                        r_SM_Main <= s_TX_Start_Bit;
                        r_TX_Active <= '1';
                    else
                        r_SM_Main <= s_Idle;
                    end if;

                -- 2. START BIT
                when s_TX_Start_Bit =>
                    r_TX_Active <= '1';
                    r_TX_Serial <= '0';
                    
                    if r_Clk_Count < CLKS_PER_BIT-1 then
                        r_Clk_Count <= r_Clk_Count + 1;
                        r_SM_Main   <= s_TX_Start_Bit;
                    else
                        r_Clk_Count <= 0;
                        r_SM_Main   <= s_TX_Data_Bits;
                    end if;

                -- 3. DATA BIT
                when s_TX_Data_Bits =>
                    r_TX_Active <= '1';
                    r_TX_Serial <= r_TX_Data(r_Bit_Index);
                    
                    if r_Clk_Count < CLKS_PER_BIT-1 then
                        r_Clk_Count <= r_Clk_Count + 1;
                        r_SM_Main   <= s_TX_Data_Bits;
                    else
                        r_Clk_Count <= 0;
                        
                        if r_Bit_Index < 7 then
                            r_Bit_Index <= r_Bit_Index + 1;
                            r_SM_Main   <= s_TX_Data_Bits;
                        else
                            r_Bit_Index <= 0;
                            r_SM_Main   <= s_TX_Stop_Bit;
                        end if;
                    end if;

                -- 4. STOP BIT
                when s_TX_Stop_Bit =>
                    r_TX_Active <= '1';
                    r_TX_Serial <= '1';
                    
                    if r_Clk_Count < CLKS_PER_BIT-1 then
                        r_Clk_Count <= r_Clk_Count + 1;
                        r_SM_Main   <= s_TX_Stop_Bit;
                    else
                        r_TX_Done   <= '1';
                        r_Clk_Count <= 0;
                        r_SM_Main   <= s_Cleanup;
                    end if;

                -- 5. Cleanup
                when s_Cleanup =>
                    r_TX_Active <= '0';
                    r_TX_Done   <= '1';
                    r_SM_Main   <= s_Idle;
                    
                when others =>
                    r_SM_Main <= s_Idle;
            end case;
        end if;
    end process;

    o_TX_Active <= r_TX_Active;
    o_TX_Serial <= r_TX_Serial;
    o_TX_Done   <= r_TX_Done;
    
end RTL;
